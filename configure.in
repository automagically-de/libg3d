
AC_INIT(configure.in)
AC_CONFIG_AUX_DIR(config)

VERSION=0.0.5
PACKAGE=libg3d

AM_INIT_AUTOMAKE($PACKAGE, $VERSION, no-define)
AC_SUBST(VERSION)

AC_CONFIG_HEADERS(include/g3d/config.h)

AC_PROG_CC
AM_PROG_LEX
AC_HEADER_STDC

AC_DISABLE_STATIC
AC_PROG_LIBTOOL

# gtk-doc
GTK_DOC_CHECK

# glib
AM_PATH_GLIB_2_0(2.0.0,,AC_MSG_ERROR([glib >= 2.0 is needed]), gmodule gobject)

# gdk-pixbuf
AM_PATH_GTK_2_0(2.0.0, have_gtk2=true)
if test "x$have_gtk2" = "xtrue" ; then
GDKPIXBUF_CFLAGS=$GTK_CFLAGS
GDKPIXBUF_LIBS=$GTK_LIBS
AC_SUBST(GDKPIXBUF_CFLAGS)
AC_SUBST(GDKPIXBUF_LIBS)
fi
AM_CONDITIONAL([HAVE_GDKPIXBUF], test "x$have_gtk2" = "xtrue")

# libxml2
AM_PATH_XML2(2.0.0, have_libxml2=true)
if test "x$have_libxml2" = "xtrue" ; then
LIBXML2_CFLAGS=$XML_CPPFLAGS
LIBXML2_LIBS=$XML_LIBS
AC_SUBST(LIBXML2_CFLAGS)
AC_SUBST(LIBXML2_LIBS)
fi
AM_CONDITIONAL([HAVE_LIBXML2], test "x$have_libxml2" = "xtrue")

# libmagic
AC_ARG_ENABLE(libmagic,
	[[  --disable-libmagic      disable file type detection using libmagic]],
	[if test "$enableval" = "no" ; then disable_libmagic=true ; fi])
if test "x$disable_libmagic" != "xtrue" ; then
AC_CHECK_HEADER([magic.h], have_magic_h=true)
AC_CHECK_LIB([magic], [magic_open], have_magic_open=true)
if test "x$have_magic_h" = "xtrue" && test "x$have_magic_open" = "xtrue"; then
have_libmagic=true
AC_DEFINE([USE_LIBMAGIC], 1, [use libmagic for file type detection])
AC_DEFINE_DIR([MAGIC_FILENAME], libdir/$PACKAGE/libg3d.magic, [magic file])
MAGIC_LIBS=-lmagic
fi
fi
AC_SUBST(MAGIC_LIBS)
#AM_CONDITIONAL([USE_LIBMAGIC], test "x$have_libmagic" = "xtrue")

# Debug level (verbosity & optimization)
AC_MSG_CHECKING([for debug level])
AC_ARG_ENABLE(debug, 
	[[  --enable-debug[=LVL]    enable debug level LVL or 1 (default is 0)]],
	[if test "$enableval" = "yes" ; then DEBUG=1; else DEBUG=$enableval; fi],
	DEBUG=0)
AC_MSG_RESULT($DEBUG)
AC_SUBST(DEBUG)

AC_ARG_ENABLE(experimental,
	[[  --enable-experimental   enable experimental and unfinished plugins]],
	[if test "x$enableval" = "xyes" ; then experimental=1
		else experimental=0; fi],
	experimental=0)
AM_CONDITIONAL([EXPERIMENTAL], test "x$experimental" = "x1")


if test "$DEBUG" = "0"; then
	DBG_CFLAGS="-O2"
else
	DBG_CFLAGS="-g"
fi
AC_SUBST(DBG_CFLAGS)

# plugin directories
AC_DEFINE_DIR(PLUGIN_DIR, libdir/$PACKAGE/plugins, [Plugin directory])

PLUGINS_LDFLAGS="-avoid-version -export-dynamic"
AC_SUBST(PLUGINS_LDFLAGS)

AC_OUTPUT(
	Makefile
	doc/Makefile
	doc/api/Makefile
	include/Makefile
	include/g3d/Makefile
	m4/Makefile
	plugins/Makefile
	plugins/image/Makefile
	plugins/import/Makefile
	src/Makefile
	tests/Makefile
	libg3d.pc
)

